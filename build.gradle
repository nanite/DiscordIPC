import java.time.LocalDate

plugins {
    id 'java'
    id 'maven-publish'
}

group = 'dev.nanite.forks'

def tagVersion = "${LocalDate.now().getYear()}-SNAPSHOT"
if (providers.environmentVariable("TAG_NAME").isPresent()) {
    tagVersion = providers.environmentVariable("TAG_NAME").get()
    if (tagVersion.startsWith("v")) {
        tagVersion = tagVersion.substring(1)
    }
}

version = tagVersion

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.slf4j:slf4j-api:2.0.17'
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    // Target Java 17
    options.release = 17
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    def mavenToken = providers.environmentVariable("NANITE_DEPLOY")

    repositories {
        if (mavenToken.isPresent()) {
            maven {
                url = uri("https://maven.nanite.dev/releases")
                credentials {
                    username = "nanite"
                    password = mavenToken.get()
                }
            }
        }
    }
}
